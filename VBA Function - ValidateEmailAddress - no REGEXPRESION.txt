-- 1. REG EXPRESSION: https://www.devhut.net/2010/06/22/vba-regular-rxpressions-regex/ 
-- 2. REGULAR EXPRESSION: http://regexlib.com/?AspxAutoDetectCookieSupport=1


'---------------------------------------------------------------------------------------
' Procedure : isValidEmail
' Author    : Unknown
' Modified by: Daniel Pineault, CARDA Consultants Inc.
' Website   : http://www.cardaconsultants.com
' Purpose   : Validate an e-mail address against some basic formatting rules
'              returns True/False indicating if the e-mail address is properly formatted
'
' Input Variables:
' ~~~~~~~~~~~~~~~~
' sEmailAddress : E-mail address to validate
'
' Usage:
' ~~~~~~
' isValidEmail("@.") -> False with error message
' isValidEmail("chantal@hotmail.com") -> True
'
' Revision History:
' Rev       Date(yyyy/mm/dd)        Description
' **************************************************************************************
' 1
' 2         2017-01-26              Added Error Handler
'                                   Concatenation of error messages into a single msg
'---------------------------------------------------------------------------------------
Public Function isValidEmail(sEmailAddress As String) As Boolean
' Author: Unknown
' Modified by: Daniel Pineault
    On Error GoTo Error_Handler
    Dim sMsg                  As String
 
    isValidEmail = True
 
    If (Len(sEmailAddress) = 0) Then
        sMsg = "Please enter your email address."
        isValidEmail = False
    End If
 
    If (InStr(1, sEmailAddress, "@") = 0) Then
        sMsg = sMsg & IIf(sMsg = "", "", vbCrLf) & "The '@' is missing from your e-mail address."
        isValidEmail = False
    End If
 
    If (InStr(1, sEmailAddress, ".") = 0) Then
        sMsg = sMsg & IIf(sMsg = "", "", vbCrLf) & "The '.' is missing from your e-mail address."
        isValidEmail = False
    End If
 
    If (InStr(sEmailAddress, "@.") > 0) Then
        sMsg = sMsg & IIf(sMsg = "", "", vbCrLf) & "There is nothing between '@' and '.'"
        isValidEmail = False
    End If
 
    If ((InStr(sEmailAddress, ".")) = ((Len(sEmailAddress)))) Then
        sMsg = sMsg & IIf(sMsg = "", "", vbCrLf) & "There has to be something after the '.'"
        isValidEmail = False
    End If
 
    If ((Len(sEmailAddress)) < (InStr(sEmailAddress, ".") + 2)) Then
        sMsg = sMsg & IIf(sMsg = "", "", vbCrLf) & "There should be two letters after the '.'"
        isValidEmail = False
    End If
 
    If (InStr(1, sEmailAddress, "@") = 1) Then
        sMsg = sMsg & IIf(sMsg = "", "", vbCrLf) & "You have to have something before the '@'"
        isValidEmail = False
    End If
 
    'Display the error message if applicable
    If isValidEmail = False Then
        Call MsgBox(sMsg, vbCritical + vbOKOnly, "E-mail Address Syntax Error")
    End If
 
Error_Handler_Exit:
    On Error Resume Next
    Exit Function
 
Error_Handler:
    MsgBox "The following error has occured" & vbCrLf & vbCrLf & _
           "Error Number: " & Err.Number & vbCrLf & _
           "Error Source: isValidEmail" & vbCrLf & _
           "Error Description: " & Err.Description & _
           Switch(Erl = 0, "", Erl <> 0, vbCrLf & "Line No: " & Erl) _
           , vbOKOnly + vbCritical, "An Error has Occured!"
    Resume Error_Handler_Exit
End Function